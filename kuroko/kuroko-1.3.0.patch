diff -Naur kuroko-1.3.0/meson.build kuroko-1.3.0-patched/meson.build
--- kuroko-1.3.0/meson.build	1970-01-01 01:00:00.000000000 +0100
+++ kuroko-1.3.0-patched/meson.build	2022-09-16 03:31:44.287728211 +0100
@@ -0,0 +1,23 @@
+project('kuroko', 'c', version : '1.3.0', license : 'MIT')
+
+cc = meson.get_compiler('c')
+dl_dep = cc.find_library('dl', required : true)
+rt_dep = cc.find_library('rt', required : true)
+
+linkdeps = [dl_dep, rt_dep]
+
+subdir('src')
+
+# Having src as an include directory is required so <kuroko/> include paths work fine.
+srcinc = include_directories('src')
+
+# Kuroko exports all symbols
+executable('kuroko', 'src/kuroko.c', export_dynamic : true,
+           include_directories : srcinc, link_with : libkuroko.get_static_lib(),
+           install : true, dependencies : linkdeps)
+
+install_subdir('modules', install_dir : 'lib/kuroko/', strip_directory : true)
+
+# Generate a trivial pkgconfig file
+pkg = import('pkgconfig')
+pkg.generate(libkuroko, name : 'libkuroko', description : 'Kuroko language interpreter')
diff -Naur kuroko-1.3.0/src/kuroko/meson.build kuroko-1.3.0-patched/src/kuroko/meson.build
--- kuroko-1.3.0/src/kuroko/meson.build	1970-01-01 01:00:00.000000000 +0100
+++ kuroko-1.3.0-patched/src/kuroko/meson.build	2022-09-16 03:37:16.137749337 +0100
@@ -0,0 +1,14 @@
+headers = ['chunk.h',
+'compiler.h',
+'debug.h',
+'kuroko.h',
+'memory.h',
+'object.h',
+'scanner.h',
+'table.h',
+'threads.h',
+'util.h',
+'value.h',
+'vm.h',]
+
+install_headers(headers, subdir : 'kuroko')
diff -Naur kuroko-1.3.0/src/meson.build kuroko-1.3.0-patched/src/meson.build
--- kuroko-1.3.0/src/meson.build	1970-01-01 01:00:00.000000000 +0100
+++ kuroko-1.3.0-patched/src/meson.build	2022-09-16 03:36:42.024071167 +0100
@@ -0,0 +1,43 @@
+libkuroko_sources = ['builtins.c',
+'chunk.c',
+'compiler.c',
+'debug.c',
+'exceptions.c',
+'fileio.c',
+'kuroko.c',
+'memory.c',
+'obj_base.c',
+'obj_bytes.c',
+'obj_dict.c',
+'object.c',
+'obj_function.c',
+'obj_gen.c',
+'obj_list.c',
+'obj_long.c',
+'obj_numeric.c',
+'obj_range.c',
+'obj_set.c',
+'obj_slice.c',
+'obj_str.c',
+'obj_tuple.c',
+'obj_typing.c',
+'os.c',
+'parseargs.c',
+'scanner.c',
+'sys.c',
+'table.c',
+'threads.c',
+'time.c',
+'value.c',
+'vm.c',
+'vendor/rline.c']
+
+inc = include_directories('kuroko')
+
+srcinc = include_directories('.')
+
+subdir('kuroko')
+
+libkuroko = both_libraries('kuroko', libkuroko_sources, include_directories : inc, install : true, dependencies : linkdeps)
+
+subdir('modules')
diff -Naur kuroko-1.3.0/src/modules/meson.build kuroko-1.3.0-patched/src/modules/meson.build
--- kuroko-1.3.0/src/modules/meson.build	1970-01-01 01:00:00.000000000 +0100
+++ kuroko-1.3.0-patched/src/modules/meson.build	2022-09-16 03:31:44.287728211 +0100
@@ -0,0 +1,13 @@
+list_of_modules = ['math', 'random', 'socket', 'timeit', 'wcwidth']
+
+# The list of modules above could easily be augmented if we wanted modules with more than a single
+# file
+
+foreach module : list_of_modules
+    deps = linkdeps
+    if module == 'math'
+        deps += cc.find_library('m', required : true)
+    endif
+    shared_module(module, 'module_' + module + '.c', include_directories : srcinc, install : true,
+                  name_prefix : '', install_dir : 'lib/kuroko', dependencies : deps)
+endforeach
